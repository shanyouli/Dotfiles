{
  pkgs,
  lib,
  config,
  options,
  ...
}:
with lib;
with lib.my; let
  cfm = config.modules;
  cfg = cfm.dev.mise;
  cfbin = "${cfg.package}/bin/mise";
in {
  options.modules.dev.mise = with types; {
    enable = mkEnableOption "Whether to use mise";
    plugins = mkOption {
      description = "mise install plugins";
      type = attrsOf (oneOf [str (nullOr bool) (listOf str)]);
      default = {};
    };
    package = mkPkgOpt pkgs.unstable.mise "mise package";

    text = mkOpt' lines "" "init mise script";
    prevInit = mkOpt' lines "" "prev mise env";
    extInit = mkOpt' lines "" "extra mise Init";
  };
  config = mkIf cfg.enable {
    user.packages = [cfg.package];
    modules.shell.env.MISE_CACHE_DIR = "${config.home.cacheDir}/mise";
    modules.shell.rcInit = ''_cache -v ${cfg.package.version} mise activate zsh'';
    modules.dev.mise.text = let
      mise_core_plugins = ["python" "bun" "deno" "erlang" "go" "java" "ruby" "rust" "node"];
      finalNeedPlugins = lib.filterAttrs (k: v: !(builtins.elem v [null false])) cfg.plugins;
      mise_in_plugin_fn = v: ''${cfbin} p add ${v} -y'';
      mise_plugin_ver_fn = p: vers:
        if builtins.isString vers
        then ''
          echo-info "Use ${p} ${vers} ..."
          ${cfbin} install ${p}@${vers}
        ''
        else
          concatStrings (map (v: ''
              echo-info "Use ${p} ${v} ..."
              ${cfbin} install ${p}@${v}
            '')
            vers);
      text = concatStringsSep "\n" (mapAttrsToList (n: v: ''
          echo-info "Using mise to manage versions of ${n}"
          ${lib.optionalString (v != true) ''${mise_plugin_ver_fn n v}''}
          ${lib.optionalString (! elem n mise_core_plugins) ''${mise_in_plugin_fn n}''}
        '')
        finalNeedPlugins);
    in ''
      ${cfg.prevInit}
      export MISE_CACHE_DIR="${config.home.cacheDir}/mise"
      ${text}
      ${cfg.extInit}
    '';
    modules.shell.direnv.stdlib.mise = optionalNull cfm.shell.direnv.enable pkgs.writeScript "use_mise" ''
      #!/usr/bin/env sh
      ### Do not edit. This was autogenerated by 'mise direnv' ###
      use_mise() {
        direnv_load mise direnv exec
      }
    '';
    modules.shell.nushell.cacheCmd = ["${cfbin} activate nu"];
  };
}
